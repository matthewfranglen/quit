VIM_EXE=${${:-=vim}:A}

# Turns the /proc/self link into the /proc/[NUMBER] link
# Also starts using reply, which will be the eventual return value
expand_link () {
    reply=${REPLY:A}
}

# This will take the first part of the cmdline file which will
# be the command in question. The use of sudo is not reflected
# in this file.
# See: http://stackoverflow.com/questions/1585989/how-to-parse-proc-pid-cmdline
command_name () {
    # This actually has a terrible time with the shells started by tmux.
    # When tmux starts my zsh, the command name is -zsh.
    # It seems to be impossible to suppress the error for the inability to expand -zsh using =
    echo =${(s/\0/)$(cat $reply/cmdline)[1]}
}

# Checks to see if the process is the vim process.
# The matching would be better done using exe, which is a link
# to the actual command file, but that is difficult to dereference.
is_vim () {
    [[ $(command_name) = $VIM_EXE ]]
    # ${reply}/exe is frequently impossible to dereference
    # [ ${reply}/exe(:A) = $VIM_EXE ]
}

# Checks to see if the process is the init process.
# If you get to the init process then the search has failed.
is_init () {
    [ ${reply:t} -eq 1 ]
    # [ ${reply}/exe(:A) = "/sbin/init" ]
}

# Look up /proc/[number]/stat in man 5 proc
get_parent () {
    reply=/proc/${(s/ /)$(cat ${reply}/stat)[4]}
}

# This is the recursive function.
# This stops if the vim process is found, or if the init process has been reached.
# Otherwise it checks the parent of the current process.
rc_get_vim_process() {
    if is_vim
    then
        return 0
    elif is_init
    then
        return 1
    fi

    get_parent
    rc_get_vim_process
}

# Expands the /proc/self link and then searches for the vim parent
get_vim_process () {
    expand_link
    rc_get_vim_process
}

# vim: set ai et sw=4 syntax=zsh :
